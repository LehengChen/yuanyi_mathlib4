/-
Copyright (c) 2025 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Floris van Doorn
-/
module

public import Mathlib.CategoryTheory.Limits.Opposites
public import Mathlib.CategoryTheory.Limits.Filtered

/-!
# Filtered colimits and cofiltered limits in `C` and `Cᵒᵖ`

We construct filtered colimits and cofiltered limits in the opposite categories.

-/

@[expose] public section

universe v₁ v₂ u₁ u₂

noncomputable section

open CategoryTheory

open CategoryTheory.Functor

open Opposite

namespace CategoryTheory.Limits

variable {C : Type u₁} [Category.{v₁} C]
variable {J : Type u₂} [Category.{v₂} J]

instance has_cofiltered_limits_op_of_has_filtered_colimits [HasFilteredColimitsOfSize.{v₂, u₂} C] :
    HasCofilteredLimitsOfSize.{v₂, u₂} Cᵒᵖ where
  HasLimitsOfShape _ _ _ := hasLimitsOfShape_op_of_hasColimitsOfShape

theorem has_cofiltered_limits_of_has_filtered_colimits_op
    (h : ∀ (I : Type u₂) [Category.{v₂} I] [IsCofiltered I],
      HasColimitsOfShape Iᵒᵖ Cᵒᵖ) :
    HasCofilteredLimitsOfSize.{v₂, u₂} C :=
  { HasLimitsOfShape := fun I _ _ =>
      haveI : HasColimitsOfShape Iᵒᵖ Cᵒᵖ := h I
      hasLimitsOfShape_of_hasColimitsOfShape_op }

instance has_filtered_colimits_op_of_has_cofiltered_limits [HasCofilteredLimitsOfSize.{v₂, u₂} C] :
    HasFilteredColimitsOfSize.{v₂, u₂} Cᵒᵖ where HasColimitsOfShape _ _ _ := inferInstance

theorem has_filtered_colimits_of_has_cofiltered_limits_op
    (h : ∀ (I : Type u₂) [Category.{v₂} I] [IsFiltered I],
      HasLimitsOfShape Iᵒᵖ Cᵒᵖ) :
    HasFilteredColimitsOfSize.{v₂, u₂} C :=
  { HasColimitsOfShape := fun I _ _ =>
      haveI : HasLimitsOfShape Iᵒᵖ Cᵒᵖ := h I
      hasColimitsOfShape_of_hasLimitsOfShape_op }

end CategoryTheory.Limits
